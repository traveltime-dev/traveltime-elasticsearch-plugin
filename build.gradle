buildscript {
    repositories {
        gradlePluginPortal()
    }

    dependencies {
        classpath "io.freefair.gradle:lombok-plugin:5.3.0"
    }
}

apply plugin: 'java-library'

def common = project(':common')

configure(subprojects - common) {
    group = 'com.traveltime'

    repositories {
        maven {
            url = 'https://snapshots.elastic.co/maven/'
        }
        mavenCentral()
    }

    apply plugin: "io.freefair.lombok"

    apply plugin: 'java'

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    dependencies {
        implementation(common) {
            exclude group: 'org.apache.logging.log4j'
        }
        implementation('com.traveltime:traveltime-sdk-java:1.1.11') {
            exclude group: 'com.fasterxml.jackson.core'
            exclude group: 'org.locationtech.jts'
        }
        implementation group: 'com.google.guava', name: 'guava', version: '31.0.1-jre'
        implementation 'io.vavr:vavr:0.10.4'
        implementation group: 'it.unimi.dsi', name: 'fastutil', version: '8.5.6'
    }

    ext.buildPlugin = { project, esVersion, esRevisions ->
        project.version = project.findProperty('tag') ?: "v0.2-SNAPSHOT"

        project.dependencies {
            compileOnly "org.elasticsearch:elasticsearch:${esVersion}.${esRevisions.findAll{ it.isInteger() }.max()}"
        }

        project.task("pluginZip")

        esRevisions.forEach { revision ->
            def realRevision = revision.replaceAll("-SNAPSHOT", "")

            project.task("makeProperties_${revision}", type: Copy) {
                from "src/universal/plugin-descriptor.properties"
                into "$buildDir/generated_${revision}"
                filter { line -> line.replaceAll('ES_VERSION', "${esVersion}.${realRevision}").replaceAll('PLUGIN_VERSION', "${version}") }
            }

            project.task("pluginZip_${revision}", dependsOn: [project.jar, "makeProperties_${revision}"], type: Zip) {
                archiveName rootProject.name + "_${project.version}_${esVersion}.${revision}.zip"
                destinationDir rootProject.distsDir
                from configurations.runtime.allArtifacts.files
                from configurations.runtimeClasspath
                from "$buildDir/generated_${revision}" include '*'
                from "src/universal/plugin-security.policy"
            }

            project.pluginZip.dependsOn("pluginZip_${revision}")
        }

        task("Dockerfile", type: Copy) {
            from "${common.projectDir.path}/src/universal/Dockerfile.template"
            into buildDir
            rename { "Dockerfile" }
            filter { line -> line.replaceAll('ES_VERSION', "${esVersion}") }
        }

        task("dockerignore", type: Copy) {
            from "${common.projectDir.path}/src/universal/.dockerignore.template"
            into buildDir
            rename { ".dockerignore" }
            filter { line -> line.replaceAll('ES_VERSION', "${esVersion}") }
        }

        task("testZip", type: Copy, dependsOn: "pluginZip_0") {
            from "${rootProject.distsDir}/${rootProject.name + "_${project.version}_${esVersion}.0.zip"}"
            into project.distsDir
        }

        task("dockerBuildTest", type: Exec, dependsOn: ["Dockerfile", "dockerignore", "testZip"]) {
            environment "DOCKER_BUILDKIT", "1"
            commandLine "docker", "build", ".", "-f", "build/Dockerfile", "-t", "${project.version}_${esVersion}-test"
        }
    }
}

