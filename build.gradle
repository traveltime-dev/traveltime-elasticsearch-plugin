buildscript {
    repositories {
        gradlePluginPortal()
    }

    dependencies {
        classpath "io.freefair.gradle:lombok-plugin:5.3.0"
    }
}

def esMajorMinor = '7.10'
def esRevisions = ['0', '1', '2', '3-SNAPSHOT']
def realMax = esRevisions.findAll{it.isInteger() }.max()

group = 'com.traveltime'
def compositeVersion = (project.findProperty('tag') ?: "v0.2-SNAPSHOT_${esMajorMinor}.${realMax}").toString()
version = compositeVersion.split('_')[0]

repositories {
    maven {
        url = 'https://snapshots.elastic.co/maven/'
    }
    mavenCentral()
}

apply plugin: "io.freefair.lombok"

apply plugin: 'java'

compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
    compileOnly "org.elasticsearch:elasticsearch:${esMajorMinor}.${realMax}"
    implementation('com.traveltime:traveltime-sdk-java:1.1.10') {
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'org.locationtech.jts'
    }
    implementation group: 'com.google.guava', name: 'guava', version: '31.0.1-jre'
    implementation 'io.vavr:vavr:0.10.4'
    implementation group: 'it.unimi.dsi', name: 'fastutil', version: '8.5.6'
}

task pluginZip

esRevisions.forEach { revision ->
    def realRevisions = revision.replaceAll("-SNAPSHOT", "")

    project.task("makeProperties_${revision}", type: Copy) {
        from "src/universal/plugin-descriptor.properties"
        into "$buildDir/generated_${revision}"
        filter { line -> line.replaceAll('ES_VERSION', "${esMajorMinor}.${realRevisions}").replaceAll('PLUGIN_VERSION', "${version}") }
    }

    project.task("pluginZip_${revision}", dependsOn: [jar, "makeProperties_${revision}"], type: Zip) {
        archiveName project.name + "_${esMajorMinor}.${revision}_${project.version}.zip"
        from configurations.runtime.allArtifacts.files
        from configurations.runtimeClasspath
        from "$buildDir/generated_${revision}" include '*'
        from "src/universal/plugin-security.policy"
    }

    pluginZip.dependsOn("pluginZip_${revision}")
}
